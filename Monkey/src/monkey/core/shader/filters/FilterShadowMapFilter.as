package monkey.core.shader.filters {
	import flash.geom.Matrix3D;
	
	import monkey.core.camera.Camera3D;
	import monkey.core.shader.utils.FcRegisterLabel;
	import monkey.core.shader.utils.FsRegisterLabel;
	import monkey.core.shader.utils.ShaderRegisterCache;
	import monkey.core.shader.utils.ShaderRegisterElement;
	import monkey.core.shader.utils.VcRegisterLabel;
	import monkey.core.textures.Texture3D;
	import monkey.core.utils.Device3D;
	
	/**
	 * äºŒæ¬¡çº¿æ€§æ’å€¼è¿‡æ»¤
	 * å¯¹é˜´å½±è´´å›¾è¿›è¡Œé‡‡æ ·æ—¶,æŠ•å½±çº¹ç†åæ ‡ (ğ‘¢, ğ‘£) é€šå¸¸ä¸ä¼šä¸é˜´å½±è´´å›¾ä¸­çš„çº¹ç†å…ƒç´ ä¸€ä¸€å¯¹åº”ã€‚
	 * è¿™äº›çº¹ç†åæ ‡é€šå¸¸ ä¼šè½åœ¨ 4 ä¸ªçº¹ç†å…ƒç´ ä¹‹é—´ã€‚æˆ‘ä»¬å¯ä»¥é€šè¿‡ä½¿ç”¨äºŒæ¬¡çº¿æ€§æ’å€¼è§£å†³è¿™ä¸€é—®é¢˜ã€‚
	 * ä¸è¿‡,ä¸åº”è¯¥è®¡ç®—æ·±åº¦å¹³å‡å€¼,å› ä¸ºè¿™ä¼šå¯¼è‡´é”™è¯¯çš„ç»“æœ,ä½¿æœ¬æ¥æ²¡æœ‰é˜´å½±çš„åœ°æ–¹å‡ºç°é˜´å½±ã€‚
	 * (å‡ºäºåŒæ ·çš„åŸå› ,ä¹Ÿä¸èƒ½ä¸ºé˜´å½±è´´å›¾ç”Ÿæˆå¤šçº§æ¸è¿‘çº¹ç†å±‚ã€‚)æ‰€ä»¥, åº”è¯¥å¯¹æ·±åº¦è´´å›¾çš„é‡‡æ ·ç»“æœè¿›è¡Œæ’å€¼,
	 * è€Œä¸æ˜¯å¯¹æ·±åº¦è´´å›¾ è¿›è¡Œæ’å€¼ã€‚ä¹Ÿå°±æ˜¯,æˆ‘ä»¬ä½¿ç”¨ç‚¹è¿‡æ»¤
	 * å¯¹åæ ‡ (ğ‘¢, ğ‘£)ã€(ğ‘¢ + âˆ†ğ‘¥, ğ‘£)ã€(ğ‘¢, ğ‘£ + âˆ†ğ‘¥)ã€(ğ‘¢ + âˆ†ğ‘¥, ğ‘£ + âˆ†ğ‘¥) è¿›è¡Œé‡‡æ ·,
	 * å…¶ä¸­ Î”ğ‘¥ = 1/SHADOW_MAP_SIZEã€‚ç”±æ­¤,è¿™ 4 ä¸ªç‚¹ä¼šåˆ†åˆ«å‘½ä¸­ä¸ (ğ‘¢, ğ‘£) è·ç¦»æœ€è¿‘çš„ 4 ä¸ªçº¹ç†å…ƒç´ 
	 *  ğ¬0ã€ ğ¬1ã€ğ¬2ã€ğ¬3,ç„¶å,æˆ‘ä»¬å¯¹è¿™äº›é‡‡æ ·å€¼è¿›è¡Œé˜´å½±è´´å›¾æµ‹è¯•,
	 * @author neil
	 *
	 */
	public class FilterShadowMapFilter extends Filter3D {
		
		private var _epsilon 		: Number = 0.01;
		private var _lightViewProj	: Matrix3D = new Matrix3D();
		private var _depthMapAxe 	: ShaderRegisterElement;
		private var _filterData 	: Vector.<Number>;				// äºŒæ¬¡çº¿æ€§è¿‡æ»¤æ•°æ®
		private var _decRGBData 	: Vector.<Number>;				// rgbè§£ç æ•°æ®
		private var _projTxtAxeData : Vector.<Number>;				// æŠ•å½±çº¹ç†åæ ‡æ•°æ®
		private var _shadowLabel	: FsRegisterLabel;
		private var _light 			: Camera3D;
		
		/**
		 * é˜´å½±shader 
		 * @param shadowmap		shadowmmapå›¾
		 * @param light			ç¯å…‰
		 * 
		 */		
		public function FilterShadowMapFilter(shadowmap : Texture3D, light : Camera3D) {
			super("FilterShadowMapFilter");
			this.priority 		= 14;
			this._shadowLabel	= new FsRegisterLabel(shadowmap);
			this._filterData 	= Vector.<Number>([0.5, shadowmap.width, 1.0 / shadowmap.width, 0]);
			this._decRGBData 	= Vector.<Number>([1.0, 1.0 / 255, 1.0 / 65025, 1.0 / 16581375]);
			this._projTxtAxeData= Vector.<Number>([0.5, -0.5, 0, 0]);
			this._light 		= light;
			this.epsilon 		= 0.0009;
		}
		
		public function get epsilon():Number {
			return _epsilon;
		}
		
		/**
		 * epsilon 
		 * @param value
		 * 
		 */		
		public function set epsilon(value:Number):void {
			this._epsilon = value;
			this._projTxtAxeData[3] = -value;
		}
		
		override public function update() : void {
			this._lightViewProj.copyFrom(Device3D.world);
			this._lightViewProj.append(_light.viewProjection);
		}
		
		override public function getFragmentCode(regCache:ShaderRegisterCache, agal:Boolean):String {
			this._depthMapAxe = regCache.getFreeV();
			// é˜´å½±å›¾
			var shadowTexture : ShaderRegisterElement = regCache.getFs(_shadowLabel);
			// rgbè§£ç 
			var decRGB : ShaderRegisterElement = regCache.getFc(1, new FcRegisterLabel(_decRGBData));
			// çº¿æ€§è¿‡æ»¤
			var filterFc0 : ShaderRegisterElement = regCache.getFc(1, new FcRegisterLabel(_filterData));
			// é˜´å½±å›¾é¢œè‰²
			var shadowColor : ShaderRegisterElement = regCache.getFt();
			// uv
			var ft0 : ShaderRegisterElement = regCache.getFt();
			// target
			var ft1 : ShaderRegisterElement = regCache.getFt();
			
			var code : String = '';
			
			if (agal) {
				code += 'mov ' + ft0 + ', ' + _depthMapAxe + '\n';
				// (ğ‘¢, ğ‘£)
				code += 'tex ' + shadowColor + ', ' + _depthMapAxe + ', ' + shadowTexture + ' <2d, nearest, clamp>\n'; // é˜´å½±å›¾é‡‡æ ·
				code += 'dp4 ' + shadowColor + '.z, ' + shadowColor + ', ' + decRGB + '\n';  // è®¡ç®—æ·±åº¦
				code += 'slt ' + ft0 + '.z, ' + _depthMapAxe + '.z, ' + shadowColor + '.z\n'; // è·å–s0
				//(ğ‘¢ + âˆ†ğ‘¥, ğ‘£)
				code += 'add ' + ft0 + '.x, ' + _depthMapAxe + '.x, ' + filterFc0 + '.z\n'; // 
				code += 'tex ' + shadowColor + ', ' + ft0 + ', ' + shadowTexture + ' <2d, nearest, clamp>\n';
				code += 'dp4 ' + shadowColor + '.z, ' + shadowColor + ', ' + decRGB + '\n';
				code += 'slt ' + ft0 + '.w, ' + _depthMapAxe + '.z, ' + shadowColor + '.z\n'; // s1
				// lerp(result0, result1, t.x)
				code += 'mul ' + shadowColor + '.x, ' + _depthMapAxe + '.x, ' + filterFc0 + '.y\n'; // t.x
				code += 'frc ' + shadowColor + '.x, ' + shadowColor + '.x\n'; 
				code += 'sub ' + ft0 + '.w, ' + ft0 + '.w, ' + ft0 + '.z\n'; // s1 - s0
				code += 'mul ' + ft0 + '.w, ' + ft0 + '.w, ' + shadowColor + '.x\n'; // (s1 - s0) * t.x
				code += 'add ' + ft1 + '.w, ' + ft0 + '.z, ' + ft0 + '.w\n'; // (s1 - s0) * t.x + s0
				// (ğ‘¢ , ğ‘£ + âˆ†y)
				code += 'mov ' + ft0 + '.x, ' + _depthMapAxe + '.x\n';
				code += 'add ' + ft0 + '.y, ' + _depthMapAxe + '.y, ' + filterFc0 + '.z\n';
				code += 'tex ' + shadowColor + ', ' + ft0 + ', ' + shadowTexture + ' <2d, nearest, clamp>\n';
				code += 'dp4 ' + shadowColor + '.z, ' + shadowColor + ', ' + decRGB + '\n';
				code += 'slt ' + ft0 + '.z, ' + _depthMapAxe + '.z, ' + shadowColor + '.z\n'; // s2
				// (ğ‘¢ + âˆ†ğ‘¥, ğ‘£ + âˆ†ğ‘¥)
				code += 'add ' + ft0 + '.x, ' + _depthMapAxe + '.x, ' + filterFc0 + '.z\n';
				code += 'tex ' + shadowColor + ', ' + ft0 + ', ' + shadowTexture + ' <2d, nearest, clamp>\n';
				code += 'dp4 ' + shadowColor + '.z, ' + shadowColor + ', ' + decRGB + '\n';
				code += 'slt ' + ft0 + '.w, ' + _depthMapAxe + '.z, ' + shadowColor + '.z\n'; // s3
				// lerp(result2, result3, t.x)
				code += 'mul ' + shadowColor + '.x, ' + _depthMapAxe + '.x, ' + filterFc0 + '.y\n'; // t.x
				code += 'frc ' + shadowColor + '.x, ' + shadowColor + '.x\n';
				code += 'sub ' + ft0 + '.w, ' + ft0 + '.w, ' + ft0 + '.z\n'; // s3 - s2
				code += 'mul ' + ft0 + '.w, ' + ft0 + '.w, ' + shadowColor + '.x\n'; // (s3 - s2) * t.x
				code += 'add ' + ft0 + '.w, ' + ft0 + '.z, ' + ft0 + '.w\n'; // (s3 - s2) * t.x + s2
				// lerp(lerp(result0, result1, t.x), lerp(result2, result3, t.x), t.y)
				code += 'mul ' + shadowColor + '.x, ' + _depthMapAxe + '.y, ' + filterFc0 + '.y\n'; // t.y
				code += 'frc ' + shadowColor + '.x, ' + shadowColor + '.x\n';
				code += 'sub ' + ft0 + '.w, ' + ft0 + '.w, ' + ft1 + '.w\n';
				code += 'mul ' + ft0 + '.w, ' + ft0 + '.w, ' + shadowColor + '.x\n';
				code += 'add ' + ft1 + '.w, ' + ft1 + '.w, ' + ft0 + '.w\n';
				// 
				code += 'sat ' + ft1 + '.w, ' + ft1 + '.w\n';
				code += 'slt ' + ft1 + '.z, ' + ft1 + '.w, ' + filterFc0 + '.x \n';
				code += 'mul ' + ft1 + '.z, ' + filterFc0 + '.x, ' + ft1 + '.z \n';
				code += 'slt ' + ft1 + '.w, ' + filterFc0 + '.x, ' + ft1 + '.w \n';
				code += 'add ' + ft1 + '.w, ' + ft1 + '.z, ' + ft1 + '.w \n';
				code += 'mul ' + regCache.oc + '.xyz, ' + regCache.oc + '.xyz, ' + ft1 + '.w \n';
			}
			
			regCache.removeFt(shadowColor);
			regCache.removeFt(ft0);
			regCache.removeFt(ft1);
			
			return code;
		}
		
		override public function getVertexCode(regCache:ShaderRegisterCache, agal:Boolean):String {
			var vt0 : ShaderRegisterElement = regCache.getVt();
			var dataReg : ShaderRegisterElement = regCache.getVc(1, new VcRegisterLabel(_projTxtAxeData));
			var depthMapProj : ShaderRegisterElement = regCache.getVc(4, new VcRegisterLabel(_lightViewProj));
			var code : String = '';
			if (agal) {
				code += 'm44 ' + vt0 + ', ' + regCache.op + ', ' + depthMapProj + '\n';
				code += 'div ' + vt0 + ', ' + vt0 + ', ' + vt0 + '.w\n';
				// è½¬æ¢åˆ°è´´å›¾åæ ‡ç³»
				code += 'mul ' + vt0 + '.xy, ' + vt0 + '.xy, ' + dataReg + '.xy\n'; // 0.5 -0.5
				code += 'add ' + _depthMapAxe + ', ' + vt0 + ', ' + dataReg + '.xxwz\n'; // 0.5 0.5 0 0
			}
			return code;
		}
		
		
	}
}
